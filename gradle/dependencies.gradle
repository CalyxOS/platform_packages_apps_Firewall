// To produce these binaries, in latest AOSP source tree, run
// $ m
def aospDeps = fileTree(include: [
        // For more information about this module:
        // https://android.googlesource.com/platform/frameworks/base/+/refs/tags/android-13.0.0_r35/Android.bp#373
        //
        // out/target/common/obj/JAVA_LIBRARIES/framework_intermediates/classes.jar
        'android.jar',
        // out/target/common/obj/JAVA_LIBRARIES/core-libart.com.android.art.release_intermediates/classes.jar
        'libcore.jar'
], dir: 'libs')

dependencies {
    compileOnly aospDeps

    /**
     * Dependencies in AOSP
     *
     * We try to keep the dependencies in sync with what AOSP ships as Seedvault is meant to be built
     * with the AOSP build system and gradle builds are just for more pleasant development.
     * Using the AOSP versions in gradle builds allows us to spot issues early on.
     */

    implementation('androidx.appcompat:appcompat') {
        // https://android.googlesource.com/platform/prebuilts/sdk/+/refs/tags/android-13.0.0_r35/current/androidx/Android.bp#241
        version { strictly '1.6.0-alpha05' } // 1.6.0-alpha02 in AOSP but not available
    }

    implementation('androidx.constraintlayout:constraintlayout') {
        // https://android.googlesource.com/platform/prebuilts/sdk/+/refs/tags/android-13.0.0_r3/current/extras/constraint-layout-x/Android.bp#64
        version { strictly '2.2.0-alpha05' }
    }

    implementation('androidx.core:core-ktx') {
        // https://android.googlesource.com/platform/prebuilts/sdk/+/refs/tags/android-13.0.0_r32/current/androidx/Android.bp#2275
        version { strictly '1.9.0-alpha05' } // 1.9.0-alpha03 in AOSP but has SDK version issues
    }

    implementation('com.google.android.material:material') {
        // https://android.googlesource.com/platform/prebuilts/sdk/+/refs/tags/android-13.0.0_r32/current/extras/material-design-x/Android.bp#6
        version { strictly '1.7.0-alpha03' }
    }

    /**
     * Test Dependencies (do not concern the AOSP build)
     */

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
}
